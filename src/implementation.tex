\section{Implementation}
After discussing the high-level design of the filesystem, we explore the details of the implementation, with particular focus on elements that are specific to Ada/SPARK.
The filesystem is implemented using the Ada 2012 programming language.
Some of its parts are written in the SPARK 2014 language, which is a subset of Ada that removes features not amenable to formal verification, and defines new aspects to support modular, constructive, formal verification \cite{sparkRM}.
We use the AdaCore GNAT Community 2020 package,\footnote{\url{https://www.adacore.com/community}} which provides, among others, the compiler and prover tools.
For testing purposes, a FUSE driver is written in C, and the built executables are linked with libfuse 3.9.2.\footnote{\url{https://github.com/libfuse/libfuse}}
The GNAT Project Manager is used to facilitate compilation of source files in different languages and linking of other required libraries.

\subsection{Overview}
The implementation of AdaFS has \result{loc} lines of code: \result{loc-specification} lines for the specification, and \result{loc-implementation} for the implementation.
\autoref{tab:lines of code} shows the number of lines of code in the entire project, separated by component.
The component that defines filesystem logic has the most lines of code in the specification, as it also contains the formal verification code, which is part of the specification.
The disk IO component contains the most lines of implementation code, as it takes care of the complexities of reading/writing data to disk.
The \textit{mkfs} utility does not contain any specification code, as it is not verified and it consists of a single main procedure (though it has nested procedures, those do not require a specification).

\begin{table}[tb]
  \centering
  \begin{tabular}{l | r | r}
    Component & Specification & Implementation \\
    \hline \hline
    Filesystem Logic & \result{loc-logic-specification} & \result{loc-logic-implementation} \\
    Disk IO & \result{loc-io-specification} & \result{loc-io-implementation} \\
    FUSE interaction & \result{loc-fuse-specification} & \result{loc-fuse-implementation} \\
    \textit{mkfs} utility & \result{loc-utility-specification} & \result{loc-utility-implementation}
  \end{tabular}
  \caption{Lines of code in AdaFS, separated by component.}
  \label{tab:lines of code}
\end{table}

\subsection{Language features}
Next, we outline some of Ada's features that have been particularly useful in the development of AdaFS, or that we consider unique and interesting.

\paragraph{Strong typing}
Ada is a strongly typed language, which helps the programmer distinguish between types that are logically different, even if their underlying representation is the same.
Furthermore, the compiler will automatically catch any bugs that would be caused by assigning a value of an incorrect type to a variable.
In order for a value to be assigned to a variable, two constraints must be satisfied: the value and variable must have the same type, and the value must satisfy all constraints on the variable (such as the range for an integral data type) \cite{barnes2014}.
Conversion between types is allowed, but only if it is explicit, and if the target type is an ancestor of the current type (for example, a positive integer may be converted to an integer, but not vice-versa).
An example of the use of types is the definition of a character buffer of arbitrary size, followed by the definitions of different block types including a constrained version of the buffer type, shown in \autoref{code:block type definitions}.

\begin{lstlisting}[float=tb,caption={Block type definitions}, label={code:block type definitions}, language=Ada]
type data_buf_t is array (Positive range <>) of Character;

type inode_block_t is array (1..block_size/on_disk'Size) of on_disk;
type zone_block_t is array (1..n_indirects_in_block) of Natural;
type dir_entry_block_t is array (1..block_size/direct'Size) of direct;
subtype data_block_t is data_buf_t (1..data_block_range'Last);
\end{lstlisting}

\paragraph{Controlled types}
Controlled types allow the programmer to specify precisely what happens when a variable of a given type is declared, or when it goes out of scope.
They are not permitted in SPARK, because the compiler inserts implicit calls to allow this functionality \cite{sparkRM}.
The initialization and destruction of variables of a controlled type is handled with custom \lstinline[language=Ada]{initialize} and \lstinline[language=Ada]{finalize} procedures for the type, thus the compiler needs to ensure these procedures are called in execution.
However, in AdaFS, controlled types are not used for any filesystem logic, and are only used with disk I/O to make sure that e.g. the variable containing the superblock is initialized properly with the type of I/O the filesystem uses.
Therefore, they were deemed acceptable for this prototype.

\paragraph{Lack of pointers}
The C codebase of MINIX makes extensive use of pointers to refer to inodes and other structures in memory, addresses on disk, etc.
Ada has access types, which are similar to pointers in some ways, but due to the complexity of verifying a program's behavior when it contains pointers, SPARK severely restricts the possibilities of using access types \cite{sparkRM}.
Thus, alternative methods must be used to provide similar functionality.

One way to solve this is with parameter modes.
Ada allows specifying the mode of each parameter in a procedure, which designates how the parameter will be used in execution.
If a parameter is of `in' mode, it is only read in the procedure, and is not modified -- this is the default mode.
If a parameter is of `out' mode, the value of the parameter before the call is irrelevant, as it will receive a value in the procedure.
Finally, if a parameter is of `in out' mode, it is both read and updated in the procedure.
This third mode provides functionality similar to a pointer.

However, SPARK requires that functions be purely functional; that is, they cannot have side effects, such as parameters with a mode of `out' or `in out'.
Here, two solutions are possible.
In some cases, it may be preferred to add the return value as an `out' parameter, and rewrite the function as a procedure.
In other cases, it is better for the function to return multiple values, which is possible with a record type.
An example is the function \lstinline[language=Ada]{parse_next} in \autoref{code:function returning record}, which returns two values, wrapped in the record type \lstinline[language=Ada]{parsed_res_t}.

A third solution is to simply use index values.
This was employed in the implementation of tables, such as the \textit{filp} table.
In MINIX, items in these tables are referred to using pointers, but AdaFS refers to them by index.

\begin{lstlisting}[float=tb,caption={Parse function returning the parsed component and the new cursor position (ellipses denote code omitted for brevity)}, label={code:function returning record}, language=Ada]
subtype cursor_t is Natural range path'Range;

type parsed_res_t is record
  next : adafs.name_t;
  new_cursor : cursor_t;
end record;

function parse_next
  (path : adafs.path_t; cursor : cursor_t) return parsed_res_t
is
  ...
end parse_next;
\end{lstlisting}

\paragraph{Modularisation}
Ada supports modularisation in the form of packages, and forms a single translation unit, which can contain member entities such as subprograms, variables, and types.
Information hiding is done by defining members as private.
A package is separated into two parts, which are placed in separate files: the \textit{specification} (the public interface for the package), and the \textit{body} (the implementation).
The compiler always checks whether the package body matches the specification, and refuses to compile the code if this is not the case.
\autoref{code:inode specification and body} shows an excerpt from the specification and body of the \lstinline[language=Ada]{adafs.inode} package.

A package can also have child packages: for example, the \lstinline[language=Ada]{adafs.inode} package is a child of the \lstinline[language=Ada]{adafs} package.
A child package has access to all member entities defined in the specification of its parent(s), including private members.

\begin{lstlisting}[float=tb,caption={Excerpt from the adafs.inode package specification and body (ellipses denote code omitted for brevity)}, label={code:inode specification and body}, language=Ada]
-- adafs-inode.ads
package adafs.inode
  with SPARK_Mode
is
  ...
  function calc_num_inodes_for_blocks (nblocks : Natural) return Natural
    with ...;
end package adafs.inode

-- adafs-inode.adb
package body adafs.inode
  with SPARK_Mode
is
  function calc_num_inodes_for_blocks
    (nblocks : Natural) return Natural
  is
    inode_max : constant := 65535;
    i : Natural := nblocks/3;
  begin
    ...
    return i;
  end calc_num_inodes_for_blocks;
end adafs.inode;
\end{lstlisting}

It is also possible to create \textit{generics}.
Generics are somewhat similar to objects in the Object-Oriented Programming paradigm, in that they can be instantiated with parameters.
However, an important difference is that instantiation can only occur in a declarative region (i.e. between the \lstinline[language=Ada]{is} and \lstinline[language=Ada]{begin} keywords).
Both subprograms and packages can be generic.
For example, \autoref{code:generic reading function} shows a generic function for reading data from a disk, and its instantiation.
A specification for the generic function \lstinline[language=Ada]{read_block} is defined in \textit{disk.ads}, accepting a numeric parameter between 0 and the disk size in blocks, and returning a result of the generic type \lstinline[language=Ada]{elem_t}.
In \textit{disk.adb}, the function is implemented.
If necessary, it first sets the disk file mode to read mode (\lstinline[language=Ada]{in_file}).
Then, it reads data of the generic type \lstinline[language=Ada]{elem_t} at the disk position corresponding to block number \lstinline[language=Ada]{num}, and returns these data.
The block-to-position conversion is handled by the \lstinline[language=Ada]{block2pos} function, which is omitted for brevity.
An example use of the generic function is in \textit{disk-inode.adb}, where the \lstinline[language=Ada]{read_block} function is instantiated with the data block type \lstinline[language=Ada]{data_block_t} (defined in another file to be an array of 1024 bytes).
The function instance is then used to read the data block \lstinline[language=Ada]{block_num}.

As Ada's \lstinline[language=Ada]{Stream_IO} always requires specifying the type to be read/written, using a generic function helps with code reuse.
\lstinline[language=Ada]{elem_t} is a generic type representing the type to be read or written; the concrete type is specified at instantiation.
The downside of generics is that they cannot be analyzed directly by SPARK, but must instead be verified from the context of instantiation (i.e., SPARK mode must be enabled in the package or subprogram that instantiates the generic) \cite{sparkRM}.

\begin{lstlisting}[float=tb,caption={Generic function for reading a block of type \textnormal{elem\_t}}, label={code:generic reading function}, language=Ada]
-- File: disk.ads
subtype block_num_t is Natural 0..disk_size_blocks;

generic
  type elem_t is private;
function read_block (num : block_num_t) return elem_t;

-- File: disk.adb
package SIO renames Ada.Streams.Stream_IO;
function read_block (num : block_num_t) return elem_t is
  result : elem_t;
begin
  if SIO.mode(disk) /= SIO.in_file then
    SIO.set_mode(disk, SIO.in_file);
  end if;

  SIO.set_index (disk, block2pos(num));
  elem_t'read (disk_access, result);
  return result;
end read_block;

-- File: disk-inode.adb
function read_chunk ... is
  function read_data_block is new read_block(data_block_t);
  data_block : data_block_t;
begin
  ...
  data_block := read_data_block(block_num);
  ...
end read_chunk;
\end{lstlisting}

\paragraph{Input \& output}
In AdaFS, we use a disk image file to represent the filesystem's disk.
Therefore, an appropriate way of reading and writing the file was needed.
Ada offers several types of input and output (IO), in the form of packages.
The simplest is \textit{Text\_IO}, which provides sequential file IO for human-readable text only.
The package \textit{Sequential\_IO} provides sequential access for heterogeneous data (data of varying types).
There are also two packages for random-access IO: \textit{Direct\_IO} and \textit{Stream\_IO}.
\textit{Direct\_IO} is used for files with homogeneous data (data of a single, uniform type), and \textit{Stream\_IO} is for heterogeneous data.

For this project, we required random-access IO, to allow the filesystem to read and write data at any position on the disk.
We also preferred heterogeneous IO, to be able to easily read and write data of varying types, such as different types of blocks (a data block, directory block, inode block, etc.).
Therefore, we opted for \textit{Stream\_IO}, as it best fulfilled our requirements.

\paragraph{Interfacing with other languages}
Ada has a mechanism to allow interfacing with other programming languages, such as Fortran, COBOL, or C.
This is done by replicating the types and subprogram signatures in Ada.
The Interfaces library package\footnote{\url{https://www.adaic.org/resources/add_content/standards/05aarm/html/AA-B-2.html}} provides types and subprograms for this purpose.
For example, for C, there are the Interfaces.C\footnote{\url{http://www.ada-auth.org/standards/12rm/html/RM-B-3.html}} and Interfaces.C.Strings\footnote{\url{http://www.ada-auth.org/standards/12rm/html/RM-B-3-1.html}} packages, which provide the types \lstinline[language=Ada]{chars_ptr} (mirroring \lstinline[language=C]{char*} in C), \lstinline[language=Ada]{int} (mirroring \lstinline[language=C]{int} in C), etc.
This allows \textit{exporting} subprograms from Ada, and calling them from a C program.
\autoref{code:interfacing c and ada} shows a specification of a subprogram that is exported to C by specifying the \lstinline[language=Ada]{Export} and \lstinline[language=Ada]{Convention} aspects (as well as an external name to use when calling the subprogram from C).
The subprogram is then declared as \lstinline[language=C]{extern} in C, and called from the C program's main function.
Since the main program is written in a language different from Ada, the initialization and finalization procedures (\lstinline[language=C]{adainit(void)} and \lstinline[language=C]{adafinal(void)}) must also be declared and called before and after any other Ada subprograms, respectively.
The GNAT Project Manager handles compiling and linking of files written in different languages.
Unfortunately, interfacing code is not amenable to formal verification.

\begin{lstlisting}[float=tb,caption={Interfacing code written in C and Ada. \textnormal{declarations.adb} is omitted for brevity, but is assumed to contain an implementation of the factorial function conforming to the specification.}, label={code:interfacing c and ada}, language=Ada, alsolanguage=C]
-- declarations.ads
with Interfaces.C;
package Declarations is
  function Factorial  (n : Interfaces.C.int) return Interfaces.C.int
    with Export => True,
         Convention => C,
         External_Name => "ada_factorial";
end Declarations;

-- main.c
#include <stdio.h>
extern void adainit (void);
extern void adafinal (void);
extern int ada_factorial(int n);

int main(int argc, const char *argv[]) {
  adainit();
  int n = 5;
  printf("%d\n", ada_factorial(n)); // 120
  adafinal();
}
\end{lstlisting}

\paragraph{FUSE \& the FUSE driver}
FUSE is a software interface that allows running filesystem code in userspace, with FUSE bridging the gap between the filesystem and the kernel.
This simplifies the development of filesystems, because access to the kernel and modification of kernel code is not necessary.
FUSE allows a filesystem to be developed iteratively; i.e., first it can be implemented and tested with FUSE, and later connected to a kernel if needed.

To implement a filesystem with FUSE, the code needs to be linked with the FUSE library (libfuse), and a driver must be written to specify the filesystem's handler functions for various operations.
As FUSE is written in C, the driver for AdaFS is currently also written in C, with a wrapper in Ada to convert values between C types and Ada types.
FUSE specifies a \lstinline[language=C]{struct} with pointers to functions that should be written by the programmer for the specific filesystem they are implemented.
The library also provides, among others, a function to fill file entries into a buffer, a \lstinline[language=C]{struct} to store open file information, and a function to get the context of the current operation (such as the PID requesting the operation).
\autoref{code:fuse open} shows an example from the driver, with an implementation of the open file operation.

FUSE also has support for multithreading and thus concurrent access.
However, this introduces a large amount of complexity to handle concurrent access in a filesystem.
Therefore, we run FUSE in single thread mode (with the \textit{-s} flag on the command line), which ensures that there can only be one ongoing file operation at a given time.

\begin{lstlisting}[float=tb,caption={FUSE driver implementation of \textnormal{open}}, label={code:fuse open}, language=C]
#define FUSE_USE_VERSION 31
#include <fuse.h>
// Declare the external filesystem open function written in Ada
extern int ada_open(const char *path, pid_t pid);

// The driver's open function
int adafs_open(const char *path, struct fuse_file_info *finfo) {
  pid_t pid = fuse_get_context()->pid;
  int fd = ada_open(path, pid);
  finfo->fh = fd;
  return 0;
}

// Register the function with FUSE
static struct fuse_operations adafs_ops = {
  .open = adafs_open
};

int main(int argc, char **argv) {
  ...
  return fuse_main(argc, argv, &adafs_ops, NULL);
}
\end{lstlisting}

\paragraph{Formal verification}
Unfortunately, much of the code in its current form is not amenable to formal verification.
These are namely the parts involving file input and output, and functions that work with C types (i.e. the FUSE driver).
To mitigate this, we attempted to make use of an existing project containing FUSE bindings for Ada,\footnote{\url{https://github.com/medsec/ada-fuse}} but as the project had not been maintained since 2016, we were unable to compile it.

Nevertheless, large parts of filesystem logic are formally verifiable.
Therefore, the code base was split into two distinct packages: the \textit{adafs} package, which contains filesystem logic that is strictly in the SPARK language (with the exceptions of \textit{adafs.operations}), and the \textit{disk} package, which contains unverifiable elements such as disk I/O.
The \textit{adafs} package does not include any specifics about the type of disk being used, as the \textit{disk} interface hides implementation details.
Thus, when needed, and when an alternative is found, the \textit{disk} package can simply be replaced with a verifiable implementation that exposes an identical interface.

For the parts that are formally verifiable, two types of contracts are available: functional and data contracts.
Functional contracts describe how a subprogram should function; that is, the pre- and post-conditions for a given subprogram.
They are written as boolean predicate logic expressions.
Pre-conditions are evaluated before entry into the subprogram, and post-conditions are evaluated after exit from the subprogram (and can therefore mention the subprogram's result).
SPARK can check these conditions at each call site to ensure that no subprogram call violates the conditions, and that the output(s) are shown to be conformant with the specification (the returned result for a function, and the \lstinline[language=Ada]{out} or \lstinline[language=Ada]{in out} parameters for a procedure).

The second type of contract available are data contracts.
SPARK conducts flow analysis, which models the flow of information during a subprogram's execution.
It checks for uninitialized variables, ineffective statements, and incorrect parameter modes.
It is possible to specify which global variables are read, written, or both read and written in the subprogram, using the \lstinline[language=Ada]{Global} aspect.
If no global variables are used, the value of the aspect is set to \lstinline[language=Ada]{null}.
It is also possible to specify data dependencies between a subprogram's inputs and outputs.

For example, \autoref{code:formal verification example} shows a function to get an entry from the process table, which specifies the functional and data contracts to be fulfilled for the function.
The \lstinline[language=Ada]{Global} aspect specifies that the function only depends on the value of the package variable \lstinline[language=Ada]{tab} for input.
The \lstinline[language=Ada]{Depends} aspect states that the result of the function only depends on the \lstinline[language=Ada]{tab} and \lstinline[language=Ada]{pid} variables (that is, the variable stated in the \lstinline[language=Ada]{Global} aspect, and the parameter of the function).
The post-condition states that the \lstinline[language=Ada]{is_null} component of the returned variant record will be set to True if there is no entry in \lstinline[language=Ada]{tab} for the provided PID; otherwise, the inode number of the PIDs working directory will be non-zero.
With the SPARK toolchain, we can verify that these constraints are all satisfied.
Verification is fully automatic; it does not require any human intervention.

\begin{lstlisting}[float=tb,caption={Functional and data contracts}, label={code:formal verification example}, language=Ada]
function get_entry (pid : tab_range) return entry_t with
  Global => (input => tab),
  Depends => (get_entry'Result => (tab, pid)),
  Post => (if tab(pid).is_null
           then get_entry'Result.is_null
           else get_entry'Result.workdir > 0);
\end{lstlisting}
